#!/usr/bin/perl

# --- GCodeToolbox, written by Rene K. Mueller <spiritdude@gmail.com>
#
$VERSION = '0.002';
$APPNAME = 'GCodeToolbox';
#
# History:
# 2013/02/09: 0.002: passing slicer arguments via --slicer.<key>=<value>
# 2013/02/08: 0.001: start, info and concat works

my(%value,%flag);
my %pr;

my $cmd;
my $multiply = 1;

foreach(@ARGV) {
   $value{$1} = $2, next if(/^\-\-([^\s=]+)=(\S+)/);
   $flag{$1}++, next if(/^\-(\S+)/);
   $cmd = $_, next unless($cmd);
   $multiply = $1, next if(/^(\d+)x$/);
   if(-e $_) {
      my $f = $_;
      push(@f,$f) foreach(1..$multiply);
      $multiply = 1;
      next;
   } else {
      print "ERROR: don't know what to do with '$_'\n";
      help();
   }
}

my $verbose = $flag{v};
$cmd = 'info' unless($cmd);

$value{objectSpacing} = 3 unless(defined $value{objectSpacing});
$value{slicer} = 'slic3r' unless(defined $value{slicer});

$value{printerX} = 200;
$value{printerY} = 200;
$value{printerZ} = 200;
$value{printerMargin} = 3;

if($value{printer}&&open(F,$value{printer})) {
   print STDERR "printer: $value{printer} profile\n";
   while(<F>) {
      chop;
      next if(/^\s*#/);
      if(/^\s*([^\s:=]+)\s*[:=]\s*(\S.+)\s*$/) {
         my($k,$v) = ($1,$2);
         $k =~ s/(.)/uc $1/e;
         $value{"printer$k"} = $v;
      } 
   }
   close(F);
}

help() if($#f<0); # -- nothing to do
process($cmd,@f);

sub process {
   my($cmd,@f) = @_;

   print "=== $cmd on @f\n" if($verbose);

   if($cmd eq 'cat'||$cmd eq 'concat') {
      concatObjects(@f);
   
   } elsif($cmd eq 'slice') {
      foreach my $f (@f) {
         print "=== slicing: $f (with $value{slicer})\n";
         my(@sargs);
         foreach my $k (sort keys %value) {
            push(@sargs,"--$1=".$value{"slicer.$1"}) if($k=~/^slicer\.(\S+)/);
         }
         print "execute $value{slicer} @sargs $f\n" if($verbose);
         print `$value{slicer} @sargs $f`;
      }
      
   } elsif($cmd eq 'info') {
      foreach my $f (@f) {
         my($i) = parseGCode($f);
         print "=== $f:\n";
         foreach my $k (sort keys %${i}) {
            next if($k=~/\.unit$/);
            #print sprintf("  %40s: $i->{$k}",$k);
            print "\t$k: ";
            if(ref $i->{$k} eq 'ARRAY') {
               print "array: ",@{$i->{$k}}+1," entries";
            } else {
               print $i->{$k};
               print " ".$i->{"$k.unit"} if($i->{"$k.unit"});
            }
            print "\n";
         }
      }
   } else {
      print STDERR "ERROR: unknown command '$cmd'\n";
      help();
   }
}

sub help {
   print STDERR join("\n",
      "$APPNAME $VERSION:",
      "\t-h or --help      this help",
      "\t-v                increase verbosity", 
      "\t--slicer=cmd      define slicer (default: $value{slicer})",
      "\t--slicer.[arg]=x  define slicer arguments passed on slicer (e.g. --slicer.skirts=0)",
      "\t--printer=file    reference printer profile",
      "\t--printerX=x      define build width (default: $value{printerX}) [mm]",
      "\t--printerY=y      define build depth (default: $value{printerY}) [mm]",
      "\t--printerZ=z      define build height (default: $value{printerZ}) [mm]",
      "\t--printerMargin=m define margin to perimeter of platform (default: $value{printerMargin}) [mm]",
      "\tinfo [file1]...   provide info on gcode files (default)",
      "\tslice [file1]...  slice .stl files to .gcode",
      "\tcat [file1]...    concate multiple .gcode files (stdout)",
      "\t--objectSpacing=x define distance between items when concating",
      ),"\n\n";
   exit 1;
}

sub parseGCode {
   my($f) = @_;
   my %g;
   my %h;
   
   $g{filename} = $f;
   foreach my $a (qw(x y z)) {
      foreach my $f (qw(Min Max Movement)) {
         $g{$a.$f.'.unit'} = 'mm';
      }
   }
   $g{'filamentLength.unit'} = 'mm';
   $g{'filamentVolume.unit'} = 'mm3';

   open(F,$f);
   while(<F>) {
      chop; my($l) = $_;

      $g{codeLines}++;
      print "$f: >>> $_\n" if($verbose);

      $g{"commentInfo.$1"} = $2 if(/^\s*;\s+(\S+)\s*[=:]\s*(.+)\s*$/);    # -- slic3r gives some useful metadata in the comments
      $g{comments}++, next if(/^\s*;/);

      my(@d) = split(/\s+/,$l);        # -- we decompose the line
      foreach(@d) {
         print "\t\t'$_'\n" if($verbose>1);
         if(/(M\d+)/) {
            my $c = $1;
            $g{"operations.$c.count"}++;
            $h{object} = 2 if($c eq 'M107'&&$h{object}==1);
            $h{field} = 'extruderTemperature' if($c eq 'M104');
            
         } elsif(/S([\d\.\-]+)/) {
            my($v) = $1;
            #$g{"operations.S$v.count"}++;
            $g{$h{field}."Min"} = $v if(!defined $g{$h{field}."Min"}||$g{$h{field}."Min"}>$v);
            $g{$h{field}."Max"} = $v if(!defined $g{$h{field}."Max"}||$g{$h{field}."Max"}<$v);
            push(@{$g{$h{field}}},$v);
            $h{"$h{field}Sum"} += $v; $h{"$h{field}Count"}++;
            $g{"$h{field}Average"} = $h{"$h{field}Sum"}/$h{"$h{field}Count"};

         } elsif(/(G\d+)/) {
            my $c = $1;
            $g{"operations.$c.count"}++;
            $h{object} = 1 if($c eq 'G1');
            #$h{object} = 1 if($c eq 'G92');
            #$h{object} = 1 if($c eq 'G28'&&$h{object}==0);
            $h{object} = 2 if($c eq 'G28'&&$h{object}==1);
            
         } elsif(/([XYZ])([\-\d\.]+)/) {
            my($a,$v) = ($1,$2);
            my($f) = 'movement'.$a;
            $g{$f.'Min'} = $v if(!defined $g{$f.'Min'}||$g{$f.'Min'}>$v);
            $g{$f.'Max'} = $v if(!defined $g{$f.'Max'}||$g{$f.'Max'}<$v);
            $g{$f.'Total'} += abs($v-$h{$a.'Position'});
            $g{$f.'Count'}++;
            $h{$a.'Position'} = $v;
            $h{object} = 1 if($a eq 'Z'&&$v==0);
         
         } elsif(/F([\-\d\.]+)/) {
            my($v) = $1;
            my($f) = 'feedrate';
            $g{$f.'Min'} = $v if(!defined $g{$f.'Min'}||$g{$f.'Min'}>$v);
            $g{$f.'Max'} = $v if(!defined $g{$f.'Max'}||$g{$f.'Max'}<$v);
            
         } elsif(/E([\-\d\.]+)/) {
            my($v) = $1;

            $g{filamentLength} += $v;
            $g{filamentVolume} += ($v>0?1:-1)*sq($g{"commentInfo.filament_diameter"}/2)*3.14127*$v if($g{"commentInfo.filament_diameter"});
            
            if($v>0) {                       # -- actually printing out
               foreach my $a (qw(X Y Z)) {   # -- we calculate the actual size of the object as printed
                  my $f = "object".$a;
                  my $v = $h{$a.'Position'};
                  $g{$f.'Min'} = $v if(!defined $g{$f.'Min'}||$g{$f.'Min'}>$v);
                  $g{$f.'Max'} = $v if(!defined $g{$f.'Max'}||$g{$f.'Max'}<$v);
               }
               $h{object} = 1;
            }
            
         } elsif($_ eq ';') {
            last;
         }
      }
      if($h{object}==0) {
         ;  # -- head
      } elsif($h{object}==1) {
         push(@{$g{codeObject}},$l);
      } else {
         ;  # -- tail
      }
      push(@{$g{codeRaw}},$l);
   }
   close(F);

   $g{objectX} = $g{objectXMax} - $g{objectXMin};
   $g{objectY} = $g{objectYMax} - $g{objectYMin};
   $g{objectZ} = $g{objectZMax} - $g{objectZMin};

   return \%g;
}

sub concatObjects {
   my(@f) = @_;
   my($xoff,$yoff,$ymax) = ( 0,0,0 );
   my($n,$xdir);
   
   $xoff = $value{printerMargin};
   $yoff = $value{printerMargin};
   $xdir = 1;
   
   foreach my $f (@f) {
      my($i) = parseGCode($f);
      if($xdir<0) {
         $xoff += $xdir*($i->{objectX}+$value{objectSpacing});
      }
      $n++;
      if($xdir>0) {
         if($xoff+$i->{objectX}+$value{printerMargin}>=$value{printerX}) {  
            $xoff = $value{printerX}-($value{printerMargin}+$i->{objectX});
            $yoff = $ymax+$value{objectSpacing};
            $xdir *= -1;
            #print STDERR "wrapping\n";
         }
      } else {
         if($xoff<=0) {  
            $xoff = $value{printerMargin};
            $yoff = $ymax+$value{objectSpacing};
            $xdir *= -1;
            #print STDERR "wrapping\n";
         }
      }
      if($yoff+$i->{objectY}+$value{printerMargin}>=$value{printerY}) {
         print STDERR "ERROR: object #$n, $f doesn't fit on plate anymore ($value{printerX}x$value{printerY}), disregarded\n";
         next;
      }

      print STDERR "#$n: place $f $i->{objectX}x$i->{objectY}+$xoff+$yoff (",($xdir>0?'left-to-right':'right-to-left'),")\n";
      
      reoriginObject($i,$xoff,$yoff,'keep');
      printObject($i);
      
      $ymax = $yoff+$i->{objectY} if($ymax<$yoff+$i->{objectY});
      if($xdir>0) { 
         $xoff += $xdir*($i->{objectX}+$value{objectSpacing});
      }
   }
}
      
sub reoriginObject {
   my($i,$x,$y,$z) = @_;
   my(@n);
   foreach(@{$i->{codeObject}}) {
      my(@d) = split(/\s+/);
      my(@dn);
      foreach(@d) {
         if(/([XYZ])([\d\.]+)/) {
            my($a,$v) = ($1,$2);
            my($fmin) = "object".$a."Min";
            my($fmax) = "object".$a."Max";
            $v = $v-$i->{$fmin};
            if($x ne 'keep') { $v += $x if($a eq 'X'); }
            if($y ne 'keep') { $v += $y if($a eq 'Y'); }
            if($z ne 'keep') { $v += $z if($a eq 'Z'); }
            push(@dn,$a.$v);
         } else {
            push(@dn,$_);
         }
      }
      push(@n,join(" ",@dn));
   }
   @{$i->{codeObject}} = @n;
}

sub printObject {
   my($i) = @_;
   foreach(@{$i->{codeObject}}) {
      print "$_\n";
   }
}

sub sq {
   return $_[0]*$_[0];
}
